plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id "org.openapi.generator" version "7.10.0"
}

group = 'se.digg.eudiw'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}
sourceSets.main.java.srcDir "${buildDir}/generate-resources/"

repositories {
	mavenCentral()
	mavenLocal()
	 maven {
        name = "diggsweden"
        url = "https://maven.pkg.github.com/diggsweden/eudiw-wallet-metadata-lib-poc"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
    maven {
        name = "openid-federation"
        url = "https://maven.pkg.github.com/danneleaf/openid-federation-base"
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-authorization-server'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.3'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'io.projectreactor:reactor-core:3.6.0'

	implementation 'me.paulschwarz:spring-dotenv:4.0.0'
	implementation 'commons-io:commons-io:2.18.0'
	implementation 'com.nimbusds:oauth2-oidc-sdk:11.20.1'
	implementation 'com.nimbusds:nimbus-jose-jwt:9.47'
	implementation 'com.google.zxing:core:3.5.3'
	implementation 'com.google.zxing:javase:3.5.3'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.bouncycastle:bcpkix-jdk18on:1.79'
	implementation 'org.bouncycastle:bcprov-jdk18on:1.79'
	implementation 'jakarta.annotation:jakarta.annotation-api:3.0.0'
  implementation 'se.digg.wallet:eudiw-wallet-metadata-lib-poc:1.0.0-SNAPSHOT'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	
	compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
	
	testCompileOnly 'org.projectlombok:lombok:1.18.36'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'
}

tasks.named('bootBuildImage') {
	builder = 'paketobuildpacks/builder-jammy-tiny:latest'
}

tasks.named('test') {
	useJUnitPlatform()
}
tasks.withType(Jar) {
    archiveVersion = ''
    archiveFileName = archiveClassifier.get().isEmpty() ? 
        "eudiw-wallet-issuer-poc.jar" : 
        "eudiw-wallet-issuer-poc-${archiveClassifier.get()}.jar"
}

openApiGenerate {
	generatorName = "java"
	inputSpec.set("$rootDir/endpoints-api-modified.yaml")
	ignoreFileOverride.set(".openapi-generator-java-sources.ignore")
	configOptions.put("name", "oidfed")
	invokerPackage.set('se.digg.eudiw')
	modelPackage.set('se.digg.eudiw.model')
	apiPackage.set('se.digg.eudiw.client')
	configOptions.set([
			library: 'resttemplate',
			openApiNullable: 'false',
			useJakartaEe: 'true',
			generateTests: 'false'
	])

    outputDir.set("${buildDir}/generate-resources/")
}
